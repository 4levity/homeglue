buildscript { // using old syntax for workaround, see note below
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

plugins {
    id 'java'
    id 'application'
}

// set mainClassName before applying shadow plugin, workaround for https://github.com/johnrengelman/shadow/issues/336
mainClassName = 'net.forlevity.homeglue.Main'

apply plugin: 'com.github.johnrengelman.shadow'

// generate shadow jar zip distribution (only)
tasks.withType(Tar) { enabled = false }
tasks.distZip.enabled = false
tasks.shadowDistZip.baseName = project.name

repositories {
    mavenCentral()
}

group 'net.forlevity.homeglue'
version "1.0-${'git rev-parse --short HEAD'.execute().text.trim()}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-processing', '-Xlint:-serial', '-Werror']
}

ext {
    lombokVersion = '1.18.2'
    log4j2version = '2.11.1'
    jacksonVersion = '2.9.7'
    guiceVersion = '4.2.1'
    hibernateVersion = '5.3.6.Final'
    jettyVersion = '9.4.12.v20180830'
    restEasyVersion = '3.6.1.Final'
}

dependencies {

    // lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    // logging
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4j2version
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: log4j2version
    runtimeOnly group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4j2version
    runtimeOnly group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    runtimeOnly group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: jacksonVersion
    runtimeOnly group: 'com.fasterxml.jackson.module', name: 'jackson-module-jaxb-annotations', version: jacksonVersion
    runtimeOnly group: 'com.fasterxml.jackson.jaxrs', name: 'jackson-jaxrs-json-provider', version: jacksonVersion

    // guava and guice
    implementation group: 'com.google.guava', name: 'guava', version: '26.0-jre'
    implementation group: 'com.google.inject', name: 'guice', version: guiceVersion
    implementation group: 'com.google.inject.extensions', name: 'guice-assistedinject', version: guiceVersion

    // local persistence and database
    implementation group: 'org.hibernate', name: 'hibernate-core', version: hibernateVersion
    runtimeOnly group: 'org.hibernate', name: 'hibernate-c3p0', version: hibernateVersion
    implementation group: 'com.h2database', name: 'h2', version: '1.4.197'
    implementation group: 'org.flywaydb', name: 'flyway-core', version: '5.2.0'

    // webserver and JAX-RS
    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: jettyVersion
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: jettyVersion
    runtimeOnly group: 'org.eclipse.jetty', name: 'jetty-continuation', version: jettyVersion
    runtimeOnly group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: restEasyVersion
    implementation group: 'org.jboss.resteasy', name: 'resteasy-guice', version: restEasyVersion
    runtimeOnly group: 'org.jboss.resteasy', name: 'resteasy-jackson2-provider', version: restEasyVersion

    // utilities
    implementation group: 'io.resourcepool', name: 'ssdp-client', version: '2.2.0'
    implementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.6'
    implementation group: 'io.github.classgraph', name: 'classgraph', version: '4.2.8'
    implementation group: 'com.mitchellbosecke', name: 'pebble', version: '2.4.0'
    implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.2'

    // test
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.23.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}
